import com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat

plugins {
    id "com.gradle.build-scan" version "1.9"
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.sherter.google-java-format' version '0.6'
    id 'com.jfrog.artifactory' version '4.5.2'
    id 'me.champeau.gradle.jmh' version '0.4.4'
    id 'net.researchgate.release' version '2.6.0'
    id 'maven-publish'
}

configurations {
    all*.exclude group: 'log4j', module: 'log4j'
}

repositories {
    jcenter()
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

release {
    failOnSnapshotDependencies = false
    tagCommitMessage = '[gradle-release-plugin] - creating tag: '
    newVersionCommitMessage = '[gradle-release-plugin] - new version commit: '
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 5, 'minutes'
    resolutionStrategy.cacheChangingModulesFor 30, 'minutes'
}

subprojects {
    apply plugin: 'project-report'
    apply plugin: 'idea'
    apply plugin: 'java'

    compileJava {
        options.warnings = true
        options.deprecation = true
        options.compilerArgs += ["-Xlint:unchecked"]
    }

    task format(type: GoogleJavaFormat) {}
    compileJava.dependsOn(format)

    repositories {
        maven { url 'https://dl.bintray.com/netifi/netifi-oss/' }
        mavenCentral()
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

}